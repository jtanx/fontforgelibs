# Maintainer: Jeremy Tan <jtanx@outlook.com>

_realname=cairo
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-x11"
pkgver=1.15.8
pkgrel=1
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}=$pkgver-$pkgrel")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgdesc="Cairo vector graphics library (mingw-w64)"
arch=('any')
url="https://cairographics.org"
license=(LGPL2.1 MPL1.1)
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-glib2"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-libpng"
             "${MINGW_PACKAGE_PREFIX}-freetype"
             "${MINGW_PACKAGE_PREFIX}-fontconfig"
             "${MINGW_PACKAGE_PREFIX}-libx11"
             "${MINGW_PACKAGE_PREFIX}-libxrender"
             "${MINGW_PACKAGE_PREFIX}-libxext"
             "${MINGW_PACKAGE_PREFIX}-libxcb"
            )
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-freetype"
         "${MINGW_PACKAGE_PREFIX}-fontconfig"
         "${MINGW_PACKAGE_PREFIX}-lzo2"
         "${MINGW_PACKAGE_PREFIX}-libpng"
         "${MINGW_PACKAGE_PREFIX}-pixman"
         "${MINGW_PACKAGE_PREFIX}-zlib"
         "${MINGW_PACKAGE_PREFIX}-libx11"
         "${MINGW_PACKAGE_PREFIX}-libxrender"
		 "${MINGW_PACKAGE_PREFIX}-libxext"
         "${MINGW_PACKAGE_PREFIX}-libxcb"
        )
optdepends=("${MINGW_PACKAGE_PREFIX}-glib2: libcairo-gobject")
options=('strip' 'staticlibs')
#source=("https://cairographics.org/releases/cairo-${pkgver}.tar.xz"
source=("https://cairographics.org/snapshots/cairo-${pkgver}.tar.xz"
        0009-standalone-headers.mingw.patch
        0026-create-argb-fonts.all.patch
        0030-difference-macro.mingw.patch
        0040-bool-to-winbool.mingw.patch
        0050-threading.mingw.patch)
sha256sums=('3224260a4f8e22e7ea95faf706ae111b974833dd74185be1db5ebc7618a98464'
          '234de8c5d4c28b03c19e638a353e8defb2de0367a634c002b0ea7d2877bd0756'
          '6db6c44fbdb4926d09afa978fe80430186c4b7b7d255059602b1f94c6a079975'
          '5dae0a86dcc3ded3e09dd9dbd90a11c77ed5a7f151bece00c644b39df4182d94'
          '49f9a748116a6ed9ee119a8eb0e5cb93577587051e142eae45c2886601401dd4'
          '154a9e5d9ef443f964c8112be68ebba4950c02179890f02c7c0155be358a2c59')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/0009-standalone-headers.mingw.patch
  patch -p1 -i ${srcdir}/0026-create-argb-fonts.all.patch
  patch -p1 -i ${srcdir}/0030-difference-macro.mingw.patch
  patch -p1 -i ${srcdir}/0040-bool-to-winbool.mingw.patch
  patch -p1 -i ${srcdir}/0050-threading.mingw.patch

  autoreconf -fiv
}

build() {
  # export lt_cv_deplibs_check_method='pass_all'
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  CFLAGS+=" -D_POSIX -Wno-implicit-function-declaration"
  "${srcdir}"/${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --enable-win32 \
    --enable-win32-font \
    --enable-png \
    --enable-shared \
    --enable-static \
    --enable-gobject \
    --enable-tee \
    --enable-xlib \
    --enable-xcb \
    --enable-xlib-xcb  \
    --enable-xlib-xrender \
    --disable-xcb-shm \
    --enable-fc \
    --enable-ft \
    --disable-pdf \
    --disable-svg \
    --disable-silent-rules \

  make -j$(($(nproc)+1))
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="$pkgdir" install

  # Licenses
  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING"          "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING-LGPL-2.1" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING-LGPL-2.1"
  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING-MPL-1.1"  "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING-MPL-1.1"
}
