# Maintainer: Jeremy Tan <jtanx@outlook.com>

_realname=xorg-util-macros
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=r198.39f07f7
pkgrel=1
pkgdesc="m4 macros for the Xorg packages"
arch=('any')
url="https://cgit.freedesktop.org/xorg/util/macros/"
license=("The Open Group")
makedepends=("git")
options=()
depends=()
source=("${_realname}"::"git+https://anongit.freedesktop.org/git/xorg/util/macros.git")
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_realname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd ${_realname}
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  mkdir -p build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}
  ../${_realname}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} 
  make -j$(($(nproc)+1))
}

package() {
  cd build-${MINGW_CHOST}
  make DESTDIR="${pkgdir}" install
  
  install -m755 -d "${pkgdir}${MINGW_PREFIX}/lib"
  mv "${pkgdir}${MINGW_PREFIX}/share/pkgconfig" "${pkgdir}${MINGW_PREFIX}/lib/"
  install -Dm644 "${srcdir}/${_realname}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
  install -Dm644 "${srcdir}/${_realname}/README" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/README"
}
