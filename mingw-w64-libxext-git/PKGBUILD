# Maintainer: Jeremy Tan <jtanx@outlook.com>

_realname=libxext
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=r202.490a25e
pkgrel=1
pkgdesc="X11 miscellaneous extensions library"
arch=('any')
url="http://cgit.freedesktop.org/xorg/lib/libXext/"
license=("custom")
makedepends=("git" "${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-xorg-util-macros"
             "${MINGW_PACKAGE_PREFIX}-xextproto"
             "${MINGW_PACKAGE_PREFIX}-libx11")
options=(strip staticlibs)
depends=("${MINGW_PACKAGE_PREFIX}-xextproto"
         "${MINGW_PACKAGE_PREFIX}-libx11")
source=("${_realname}"::"git+git://anongit.freedesktop.org/xorg/lib/libXext"
        "0001-libxext.patch")
sha256sums=('SKIP' 'e3ef214bf7904b0fc3f134eb3f267ab45336ee97f4355310f9aee348c9469370')

pkgver() {
  cd "$srcdir/$_realname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd ${_realname}
  patch -p1 -i ${srcdir}/0001-libxext.patch
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  mkdir -p build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}
  ../${_realname}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-static \
    --enable-shared
  make -j$(($(nproc)+1))
}

package() {
  cd build-${MINGW_CHOST}
  make DESTDIR="${pkgdir}" install
  install -Dm644 "${srcdir}/${_realname}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
  install -Dm644 "${srcdir}/${_realname}/README" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/README"
}
